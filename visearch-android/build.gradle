buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0-alpha3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'jacoco'

repositories {
    jcenter()
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 9
version = '1.0.9'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "v${versionMajor}.${versionMinor}.${versionPatch}"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile ('org.apache.httpcomponents:httpmime:4.3.5') {
        exclude module: 'httpclient'
    }

    compile 'org.apache.httpcomponents:httpclient-android:4.3.5'
    compile(group: 'com.mcxiaoke.volley', name: 'library', version: '1.0.10', ext: 'aar')

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'org.robolectric:robolectric:2.4'
}

group = 'com.visenze'


apply plugin: 'com.jfrog.bintray'

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : ''
    key = project.hasProperty('bintrayKey') ? bintrayKey : ''
    configurations = ['archives']
    pkg {
        userOrg = 'visenze'
        repo = 'maven'
        name = 'com.visenze:visearch-android'
        desc = 'ViSearch Android SDK'
        websiteUrl = 'https://github.com/visenze/visearch-sdk-android'
        vcsUrl = 'https://github.com/visenze/visearch-sdk-android'
        issueTrackerUrl = 'https://github.com/visenze/visearch-sdk-android/issues'
        licenses = ['The MIT License']
        labels = ['visenze', 'visearch', 'sdk', 'android']
        version {
            name = '1.0.9'
            description = 'ViSearch Android SDK'
            vcsTag = '1.0.9'
        }
    }
}

apply plugin: 'com.github.dcendents.android-maven'

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'visearch-android'
                description 'ViSearch Android SDK'
                url 'https://github.com/visenze/visearch-sdk-android'

                scm {
                    url 'https://github.com/visenze/visearch-sdk-android'
                    connection 'scm:git@github.com/visenze/visearch-sdk-android.git'
                    developerConnection 'scm:git@github.com/visenze/visearch-sdk-android.git'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://raw.githubusercontent.com/visenze/visearch-sdk-android/develop/LISENCE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'visenze'
                        name 'ViSenze'
                    }
                }
            }
        }.writeTo("$buildDir/pom.xml")
    }
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.source
}

task androidJavadocs(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.source
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    exclude '**/internal/**'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

artifacts {
    archives androidSourcesJar, androidJavadocsJar
}

task clearJar(type: Delete) {
    delete '../dependency/visearch-android.jar'
}

task makeJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('../dependency/')
    include('classes.jar')
    rename ('classes.jar', "visearch-android-1.0.6.jar")
}

makeJar.dependsOn(clearJar, build)